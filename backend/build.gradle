group "ru.fess38.finance"
version "0.1"

buildscript {
    ext.kotlin_version = "1.8.20"
    ext.spring_version = "5.3.26"
    ext.spring_boot_version = "2.7.10"
    ext.hibernate_version = "5.6.15.Final"
    ext.proto_version = "3.22.2"

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.9.2"
    }
}

apply plugin: "application"
apply plugin: "idea"
apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "kotlin-jpa"
apply plugin: "kotlin-spring"
apply plugin: "org.springframework.boot"
apply plugin: "com.google.protobuf"

mainClassName = "ru.fess38.finance.MainKt"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    configurations.configureEach {
        exclude group: "commons-logging"
    }

    // test
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.2"
    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
    testImplementation "org.mockito:mockito-core:4.11.0"

    // Spring
    implementation "org.springframework:spring-core:$spring_version"
    implementation "org.springframework:spring-context:$spring_version"
    implementation "org.springframework:spring-orm:$spring_version"
    implementation "org.springframework:spring-tx:$spring_version"

    // Spring Boot
    implementation "org.springframework.boot:spring-boot-starter-web:$spring_boot_version", {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-tomcat"
        exclude group: "com.fasterxml.jackson.core"
    }
    implementation "org.springframework.boot:spring-boot-starter-jetty:$spring_boot_version"
    implementation "org.springframework.boot:spring-boot-starter-security:$spring_boot_version"

    // Hibernate
    implementation "org.hibernate:hibernate-core:$hibernate_version"
    implementation "org.hibernate:hibernate-java8:$hibernate_version"

    // Protobuf
    implementation "com.google.protobuf:protobuf-java:$proto_version"
    implementation "com.googlecode.protobuf-java-format:protobuf-java-format:1.4"

    // Other
    implementation "com.amazonaws:aws-java-sdk:1.12.446"
    implementation "com.google.guava:guava:31.1-jre"
    implementation "com.typesafe:config:1.4.2"
    implementation "io.github.microutils:kotlin-logging:2.1.23"
    implementation "org.apache.commons:commons-dbcp2:2.9.0"
    implementation "com.google.api-client:google-api-client:2.2.0"
    implementation "org.apache.commons:commons-text:1.10.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.postgresql:postgresql:42.6.0"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
kotlin {
    jvmToolchain(8)
}
[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

bootJar {
    archiveFileName = "finance-0.1.jar"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$proto_version"
    }
}

sourceSets {
    main.kotlin.srcDirs += "src/main/kotlin"
    test.kotlin.srcDirs += "src/test/kotlin"
}

tasks.compileKotlin {
    dependsOn(tasks.generateProto)
}
