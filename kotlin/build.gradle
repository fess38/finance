group "ru.fess38.finance"
version "0.1"

buildscript {
  ext.kotlin_version = "1.1.4-3"
  ext.spring_version = "4.3.10.RELEASE"
  ext.spring_boot_version = "1.5.4.RELEASE"
  ext.hibernate_version = "5.1.0.Final"

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
    classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"
  }
}

apply plugin: "idea"
apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "kotlin-jpa"
apply plugin: "kotlin-spring"

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  configurations.all {
    exclude group: "commons-logging"
  }

  // test
  testCompile "junit:junit:4.12"
  testCompile "org.jetbrains.kotlin:kotlin-test"
  testCompile "org.jetbrains.kotlin:kotlin-test-junit"
  testCompile "org.mockito:mockito-all:1.10.19"

  // Spring
  compile "org.springframework:spring-core:$spring_version"
  compile "org.springframework:spring-context:$spring_version"
  compile "org.springframework:spring-orm:$spring_version"
  compile "org.springframework:spring-tx:$spring_version"

  // Spring Boot
  compile "org.springframework.boot:spring-boot-starter-web:$spring_boot_version", {
    exclude group: "org.springframework.boot", module: "spring-boot-starter-tomcat"
    exclude group: "com.fasterxml.jackson.core"
  }
  compile "org.springframework.boot:spring-boot-starter-jetty:$spring_boot_version"
  compile "org.springframework.boot:spring-boot-starter-security:$spring_boot_version"

  // Hibernate
  compile "org.hibernate:hibernate-core:$hibernate_version"
  compile "org.hibernate:hibernate-java8:$hibernate_version"

  // Other
  compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
  compile "com.zaxxer:HikariCP:2.5.1"
  compile "org.postgresql:postgresql:42.1.3"
  compile "com.google.code.gson:gson:2.8.0"
  compile "com.google.guava:guava:19.0"
  compile "com.typesafe:config:1.3.1"
  compile "com.google.api-client:google-api-client:1.22.0"
}

sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

jar {
  baseName = "finance"
  version = "0.1"
  manifest.attributes "Main-Class": "ru.fess38.finance.MainKt"

  from {
    configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
  }
}

sourceSets {
  main.kotlin.srcDirs += "src/main/kotlin"
  main.resources.srcDirs += "src/main/resources"
  test.kotlin.srcDirs += "src/test/kotlin"
  test.resources.srcDirs += "src/test/resources"
}
